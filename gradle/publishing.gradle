apply plugin: 'maven-publish'
apply plugin: 'bintray'

publishing {
	publications {
		main(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar
			artifact groovydocJar

			pom.withXml {
				asNode().with {
					appendNode('name', 'gradle-imports')
					appendNode('description', 'A Gradle plugin for organizing imports in Java and Groovy source files.')
					appendNode('url', 'http://github.com/ajoberstar/gradle-imports')
					appendNode('licenses').with {
						appendNode('license').with {
							appendNode('name', 'The Apache Software License, Version 2.0')
							appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0')
						}
					}
					appendNode('developers').with {
						appendNode('developer').with {
							appendNode('id', 'ajoberstar')
							appendNode('name', 'Andrew Oberstar')
							appendNode('email', 'andrew@ajoberstar.org')
						}
					}
					appendNode('scm').with {
						appendNode('connection', 'git://github.com/ajoberstar/gradle-imports.git')
						appendNode('developerConnection', 'git@github.com:ajoberstar/gradle-imports.git')
						appendNode('url', 'http://github.com/ajoberstar/gradle-imports')
					}
				}
			}
		}
	}
	repositories {
		mavenLocal()
	}
}

if (project.hasProperty('bintrayUser') && project.hasProperty('bintrayKey')) {
	bintray {
		user = project.bintrayUser
		key = project.bintrayKey
		publications = ['main']
		pkg {
			repo = 'gradle-plugins'
			name = 'org.ajoberstar:gradle-imports'
			desc = 'A Gradle plugin for organizing imports in Java and Groovy source files.'
			licenses = ['Apache-2.0']
			labels = ['groovy', 'java', 'gradle']
			signFiles = true
		}
	}
}
